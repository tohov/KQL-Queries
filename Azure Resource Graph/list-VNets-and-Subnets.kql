// This query lists all Virtual Networks with address spaces and their associated subnets with subnet address prefixes
// Results will also show VNet associated subscription name, and list is sorted by VNet address space and subnet address prefix
resources
| where type == 'microsoft.network/virtualnetworks'
| extend subnets = properties.subnets
| mv-expand subnets
| project VNetName = name, 
          VNetId = id, 
          VNetAddressSpace = tostring(properties.addressSpace.addressPrefixes), 
          SubnetName = subnets.name, 
          SubnetAddressPrefix = tostring(subnets.properties.addressPrefix), 
          subscriptionId
| join kind=inner (
    resourcecontainers
    | where type == 'microsoft.resources/subscriptions'
    | project subscriptionId, subscriptionName = name
) on subscriptionId
| project VNetName, VNetId, VNetAddressSpace, SubnetName, SubnetAddressPrefix, subscriptionName, subscriptionId
| order by VNetAddressSpace asc, SubnetAddressPrefix asc